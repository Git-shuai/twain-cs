;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Scannative v1.0 21-Jan-2020
; ${arg:1} - product name of scanner to open
; returns: pass or fail
;
; Scan with DAT_IMAGENATIVEXFER...
;



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Init stuff...
;
setlocal index.twpendingxfers.count '0'
setlocal index.twsetupmemxfer.preferred '2'
setlocal nativehandle '0'
setlocal imagecount '0'
setglobal g_twainstate '0'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Load and open the DSM...
;
setglobal g_twainstate '1'
dsmload
echo 'dsmload'
setglobal g_twainstate '2'
dsmentry src null dg_control dat_parent msg_opendsm hwnd
echo 'parent/opendsm.............${sts:}'
if '${sts:}' != 'SUCCESS' goto MAIN.FAIL
setglobal g_twainstate '3'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Open the driver...
;
dsmentry src null dg_control dat_identity msg_opends '0,0,0,ENGLISH_USA,USA,,2,4,0x0,,,${arg:1}'
echo 'identity/opends............${sts:} - ${ret:}'
if '${sts:}' != 'SUCCESS' goto MAIN.FAIL
setglobal g_twainstate '4'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Set ICAP_XFERMECH to TWSX_NATIVE...
;
dsmentry src null dg_control dat_capability msg_set 'ICAP_XFERMECH,TWON_ONEVALUE,TWTY_UINT16,TWSX_NATIVE'
echo 'capability/set.............${sts:} - ${ret:}'
if '${sts:}' != 'SUCCESS' goto MAIN.FAIL



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Start scanning...
;
wait reset
dsmentry src null dg_control dat_userinterface msg_enableds '0,0'
echo 'userinterface/enableds.....${sts:} - ${ret:}'
if '${sts:}' != 'SUCCESS' goto MAIN.FAIL
setglobal g_twainstate '5'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Wait for MSG_XFERREADY, if we don't see it then scoot...
;
wait 30000
echo 'wait.......................${ret:}'
if '${ret:}' !~contains 'msg_xferready' goto MAIN.PASS



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Do a native transfer...
;
:MAIN.NEXTIMAGENATIVEXFER
setglobal g_twainstate '6'
increment imagecount '${get:imagecount}' 1
dsmentry src ds dg_image dat_imagenativexfer msg_get '0'
setlocal nativehandle '${ret:}'
echo 'imagenativexfer/get........${sts:} - ${ret:}'
if '${sts:}' != 'XFERDONE' goto MAIN.FAIL
image save nativehandle native '${folder:desktop}/twaincscert/img${format:D6|${get:imagecount}}'
freehandle nativehandle
setglobal g_twainstate '7'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; End the transfer, see if we're done...
;
dsmentry src ds dg_control dat_pendingxfers msg_endxfer '0,0'
echo 'pendingxfers/endxfer.......${sts:} - ${ret:}'
if '${sts:}' != 'SUCCESS' goto MAIN.FAIL
if '${ret:${get:index.twpendingxfers.count}}' != '0' goto MAIN.NEXTIMAGENATIVEXFER
setglobal g_twainstate '5'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; We've passed...
;
:MAIN.PASS
setlocal main.result pass
goto MAIN.EXIT



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; We've run into a problem...
;
:MAIN.FAIL
echo 'ERROR...........${sts:}'
setlocal main.result fail
goto MAIN.EXIT



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Rewind the driver, close and unload the DSM, return...
;
:MAIN.EXIT
call TWAIN.REWIND
return '${get:main.result}'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Function: Rewind from any TWAIN state...
; returns: (nothing)
;
:TWAIN.REWIND
;
if '${get:g_twainstate}' < '7' goto REWIND.6
dsmentry src ds dg_control dat_pendingxfers msg_endxfer '0,0'
echo 'pendingxfers/endxfer.......${sts:}'
setglobal g_twainstate '6'
:REWIND.6
if '${get:g_twainstate}' < '6' goto REWIND.5
dsmentry src ds dg_control dat_pendingxfers msg_reset '0,0'
echo 'pendingxfers/reset.........${sts:}'
setglobal g_twainstate '5'
:REWIND.5
if '${get:g_twainstate}' < '5' goto REWIND.4
dsmentry src null dg_control dat_userinterface msg_disableds '0,0'
echo 'userinterface/disableds....${sts:}'
setglobal g_twainstate '4'
:REWIND.4
if '${get:g_twainstate}' < '4' goto REWIND.3
dsmentry src null dg_control dat_identity msg_closeds '0,0,0,ENGLISH_USA,USA,,2,4,0x0,,,${arg:1}'
echo 'identity/closeds...........${sts:}'
setglobal g_twainstate '3'
:REWIND.3
if '${get:g_twainstate}' < '3' goto REWIND.2
dsmentry src null dg_control dat_parent msg_closedsm hwnd
echo 'parent/closedsm............${sts:}'
setglobal g_twainstate '2'
:REWIND.2
if '${get:g_twainstate}' < '2' goto REWIND.1
dsmunload
echo 'dsmunload'
setglobal g_twainstate '1'
:REWIND.1
return
