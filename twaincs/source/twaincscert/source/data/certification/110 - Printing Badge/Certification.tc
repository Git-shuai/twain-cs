;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Printing Badge
;   Check if Printing is supported by the driver.  This is a badge, failure does not
;   invalidate scanner certification.  Passage adds Printing support to the cert.
;
;   Printing is complex.  This badge checks that a driver allows the basic ability to
;   print on a document.  It does not require all the possible printing features
;   described by TWAIN.
;
; Arguments
;   arg:1 - TW_IDENTITY.ProductName of scanner to test
;   arg:2 - global to set on fail
;
; Globals
;   g_localcertifyresult - global to report to caller on fail
;
; Locals
;   twidentity.productname - product name of scanner to test
;   globaltosetonfail - name says it all
;
; Returns
;   (none)
;



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Initialize stuff, load tables if not already done for us...
;
echo.titlesuite 'Printing Badge v1.0 01-Sep-2021'
echo.yellow '*************************************************************************************'
echo.yellow '*** This test is for the Printing Badge.  Failing means the driver does not       ***'
echo.yellow '*** support this functionality.  It does not prevent certification.               ***'
echo.yellow '*************************************************************************************'
;
setlocal twidentity.productname '${arg:1}'
setlocal globaltosetonfail '${arg:2}'
setglobal g_localcertifyresult 'pass' ; assume success
;
if '${get:TW_ARRAY.ItemType}' != '' goto MAIN.LOADTABLES.SKIP
run '${folder:certification}/LoadTables'
:MAIN.LOADTABLES.SKIP



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Run the tests...
;
call CERTIFY '${folder:certification}/Opends'  '${get:twidentity.productname}' 'g_localcertifyresult' 'embedded'
if '${get:g_localcertifyresult}' == 'fail' goto MAIN.CLOSEDS
;
call CERTIFY 'Printing Badge'                  '${get:twidentity.productname}' 'g_localcertifyresult'
call CERTIFY 'DAT_IMAGENATIVEXFER'             '${get:twidentity.productname}' 'g_localcertifyresult' '${folder:parent}'
call CERTIFY 'DAT_IMAGEMEMXFER'                '${get:twidentity.productname}' 'g_localcertifyresult' '${folder:parent}'
call CERTIFY 'DAT_IMAGEFILEXFER'               '${get:twidentity.productname}' 'g_localcertifyresult' '${folder:parent}'
call CERTIFY 'DAT_IMAGEMEMFILEXFER'            '${get:twidentity.productname}' 'g_localcertifyresult' '${folder:parent}'
;
:MAIN.CLOSEDS
call CERTIFY '${folder:certification}/Closeds' '${get:twidentity.productname}' 'g_localcertifyresult' 'embedded'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Summary and cleanup...
;
; Did we pass?
if '${get:g_localcertifyresult}' != 'pass' goto MAIN.ERROR
echo ''
echo.passfail 'BADGE SUMMARY' 'pass'
return 'pass'
;
; Ruh-roh...
:MAIN.ERROR
echo ''
echo.passfail 'BADGE SUMMARY' 'fail'
setglobal '${get:globaltosetonfail}' 'fail'
return 'fail'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Function: Certify
; arg:1 - script to run
; arg:2 - TW_IDENTITY.ProductName of scanner to test
; arg:3 - global value to update on fail
; arg:4 - embeded flag for open/close, or folder for DAT_IMAGE*XFER calls
; returns: (none)
;
:CERTIFY
;
; If a failure has occurred on a previous test, skip the rest of the tests,
; except for closing, that would be bad...
if '${arg:1}' ~contains 'Closeds' goto CERTIFY.RUN
if '${get:${arg:3}}' == 'fail' goto CERTIFY.FAIL
;
; Run the test...
:CERTIFY.RUN
run '${arg:1}' '${arg:2}' '${arg:4}'
if '${ret:}' != 'pass' goto CERTIFY.FAIL
return 'pass'
;
; Handle failure, set the global so we have a record of the failure...
:CERTIFY.FAIL
setglobal '${arg:3}' 'fail'
return 'fail'
