;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; TWAIN Certification Script
; This runs all of the tests needed to pass certification for TWAIN.
;
; Arguments
; arg:1 - TW_IDENTITY.ProductName of scanner to test
;
; Locals
; (none)
;
; returns: pass or fail
;



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Initialize stuff...
;
set title 'TWAIN Certification Script v1.0 12-Feb-2020'
set twidentity.productname '${arg:1}'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Initialize our report...
;
report initialize '${get:twidentity.productname}'
call ECHOANDLOG info '${get:title}'
call ECHOANDLOG info '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'
call ECHOANDLOG info 'operating system is ${platform:}'
call ECHOANDLOG info 'program is ${program:}'
call ECHOANDLOG info 'started at ${localtime:yyyy-MM-dd/hh:mm:ss}'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Run all of our tests, start by assuming success...
;
set certifyresulttwain 'pass'
call CERTIFY 'Opends' '${get:twidentity.productname}'
call CERTIFY '010 - TWAIN Standard Capability Tests/CAP_SUPPORTEDCAPS' '${get:twidentity.productname}'
call CERTIFY '010 - TWAIN Standard Capability Tests/ICAP_PIXELTYPE' '${get:twidentity.productname}'
call CERTIFY '010 - TWAIN Standard Capability Tests/ICAP_XFERMECH' '${get:twidentity.productname}'
call CERTIFY '010 - TWAIN Standard Capability Tests/DAT_CAPABILITY' '${get:twidentity.productname}'
call CERTIFY 'Closeds' '${get:twidentity.productname}'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; How did we do?
;
if '${get:certifyresulttwain}' != 'pass' goto MAIN.FAIL
call ECHOANDLOG info
call ECHOANDLOG info
call ECHOANDLOG info 'ended at ${localtime:yyyy-MM-dd/hh:mm:ss}'
call ECHOANDLOG info '**************************************'
call ECHOANDLOG info 'The scanner passed TWAIN Certification'
call ECHOANDLOG info '**************************************'
report save
echo 'report saved to ${report:}'
return 'pass'
;
:MAIN.FAIL
call ECHOANDLOG error
call ECHOANDLOG error
call ECHOANDLOG error 'ended at ${localtime:yyyy-MM-dd/hh:mm:ss}'
call ECHOANDLOG error '********************************************'
call ECHOANDLOG error 'The scanner did not pass TWAIN Certification'
call ECHOANDLOG error '********************************************'
report save
echo 'report saved to ${report:}'
return 'fail'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Function: Certify
; arg:1 - script to run
; arg:2 - scanner to test
; returns: pass or fail
;
:CERTIFY
;
run '${arg:1}' '${arg:2}'
if '${ret:}' != 'pass' goto CERTIFY.FAIL
return 'pass'
;
:CERTIFY.FAIL
set certifyresulttwain fail
return 'fail'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Function: EchoAndLog
; arg:1 - severity (normal, info, error)
; arg:2 - text
;
:ECHOANDLOG
;
if '${arg:1}' == 'info' goto ECHOANDLOG.INFO
echo.red '${arg:2}'
log error '#${arg:2}'
:ECHOANDLOG.INFO
echo.green '${arg:2}'
log info '#${arg:2}'
:ECHOANDLOG.ERROR
return
